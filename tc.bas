   10  BGCOLOR$ = "6" : FGCOLOR$ = "4" : EXTCOLS% = 0
   20  OPEN "tc.ini", AS #1
   30  IF EOF(1) = -1 THEN GOTO 120
   40  INPUT# 1, LINE$
   50  SEP% = INSTR(LINE$, "=", 0)
   60  K$ = LEFT$(LINE$, SEP%)
   70  V$ = MID$(LINE$, SEP% + 2, 90)
   80  IF K$ = "foreground" THEN FGCOLOR$ = V$
   81  IF K$ = "background" THEN BGCOLOR$ = V$
   82  IF K$ = "fg_r" THEN FG_R$ = V$
   83  IF K$ = "fg_g" THEN FG_G$ = V$
   84  IF K$ = "fg_b" THEN FG_B$ = V$
   85  IF K$ = "bg_r" THEN BG_R$ = V$
   86  IF K$ = "bg_g" THEN BG_G$ = V$
   87  IF K$ = "bg_b" THEN BG_B$ = V$
  110  GOTO 30
  120  CLOSE #1
  121  IF ARGC% = 3 THEN EXTCOLS% = 0 : GOTO 125
  122  IF FG_R$ <> "" THEN EXTCOLS% = 1
  125  IF ARGV$(1) <> "" THEN FGCOLOR$ = ARGV$(1)
  126  IF ARGV$(2) <> "" THEN BGCOLOR$ = ARGV$(2)
  130  IF BGCOLOR$ = "black" THEN BGCOLOR$ = "0"
  140  IF BGCOLOR$ = "red" THEN BGCOLOR$ = "1"
  150  IF BGCOLOR$ = "green" THEN BGCOLOR$ = "2"
  160  IF BGCOLOR$ = "yellow" THEN BGCOLOR$ = "3"
  170  IF BGCOLOR$ = "blue" THEN BGCOLOR$ = "4"
  180  IF BGCOLOR$ = "cyan" THEN BGCOLOR$ = "5"
  190  IF BGCOLOR$ = "magenta" THEN BGCOLOR$ = "6"
  200  IF BGCOLOR$ = "white" THEN BGCOLOR$ = "7"
  210  IF FGCOLOR$ = "black" THEN FGCOLOR$ = "0"
  220  IF FGCOLOR$ = "red" THEN FGCOLOR$ = "1"
  230  IF FGCOLOR$ = "green" THEN FGCOLOR$ = "2"
  240  IF FGCOLOR$ = "yellow" THEN FGCOLOR$ = "3"
  250  IF FGCOLOR$ = "blue" THEN FGCOLOR$ = "4"
  260  IF FGCOLOR$ = "cyan" THEN FGCOLOR$ = "5"
  270  IF FGCOLOR$ = "magenta" THEN FGCOLOR$ = "6"
  280  IF FGCOLOR$ = "white" THEN FGCOLOR$ = "7"
  290  PRINT "telecommander v0.7a by archer"
  300  PRINT "on startup colors can be defined:"
  310  PRINT "tc <fgcolor> <bgcolor>"
  320  PRINT "tc blue cyan"
  330  PRINT "Available Colors: black, red, green, yellow, blue, cyan, magenta, white
  340  PRINT CHR$(10);
  350  GOSUB 2160
  360  IF WIDTH < 80 OR HEIGHT < 24 THEN PRINT "This program needs atleast 80x24 :(" : END
  370  CURRENTPAGE% = 0
  380  CURRENTPOS% = 0
  390  KEY$ = ""
  400  MAXPERPAGE% = HEIGHT - 6
  410  DBL_TOP_LEFT$ = CHR$(9556)
  420  DBL_TOP_RIGHT$ = CHR$(9559)
  430  DBL_BOTTOM_LEFT$ = CHR$(9562)
  440  DBL_BOTTOM_RIGHT$ = CHR$(9565)
  450  DBL_HORIZONTAL$ = CHR$(9552)
  460  DBL_VERTICAL$ = CHR$(9553)
  470  DBL_T_RIGHT$ = CHR$(9568)
  480  DBL_T_LEFT$ = CHR$(9571)
  490  DBL_T_DOWN$ = CHR$(9574)
  500  DBL_T_UP$ = CHR$(9577)
  510  OWIDTH% = WIDTH
  520  OHEIGHT% = HEIGHT
  530  REM GOTO 780
  540  SPIN$ = "/"
  550  DELIMITER$ = " "
  560  MAXSTACK% = 0
  570  GOSUB 600
  580  GOTO 750
  590  END
  600  REM split
  610  TSTR$ = ""
  620  FOR I% = 1 TO LEN(DIR$)
  630  GOSUB 2220
  640  IF MID$(DIR$, I%, 1) = DELIMITER$ THEN GOTO 690
  650  TSTR$ = TSTR$ + MID$(DIR$, I%, 1)
  660  IF I% = LEN(DIR$) THEN GOTO 690
  670  NEXT I%
  680  RETURN
  690  SLEEP 0
  700  REM IF INSTR(TSTR$, ".txt", 0) > -1 THEN ARRAY$(MAXSTACK%) = TSTR$ : MAXSTACK% = MAXSTACK% + 1
  710  REM IF INSTR(TSTR$, ".bas", 0) > -1 THEN ARRAY$(MAXSTACK%) = TSTR$ : MAXSTACK% = MAXSTACK% + 1
  720  ARRAY$(MAXSTACK%) = TSTR$ : MAXSTACK% = MAXSTACK% + 1
  730  TSTR$ = ""
  740  GOTO 670
  750  REM PROGRAM CONTINUES HERE...
  760  PRINT CHR$(10); CHR$(10); "Press any key to continue...";
  770  MEH$ = INKEY$
  780  SHOWORHIDE% = 0 : GOSUB 2420
  781  IF EXTCOLS% = 0 THEN COLOR BGCOLOR$, FGCOLOR$
  782  IF EXTCOLS% = 1 THEN PRINT CHR$(27) + "[38;2;" + FG_R$ + ";" + FG_G$ + ";" + FG_B$ + ";48;2;" + BG_R$ + ";" + BG_G$ + ";" + BG_B$ + "m";
  790  CLS
  800  IF EXTCOLS% = 1 THEN PRINT CHR$(27) + "[38;2;" + FG_R$ + ";" + FG_G$ + ";" + FG_B$ + ";48;2;" + BG_R$ + ";" + BG_G$ + ";" + BG_B$ + "m"; : FOR CLH% = 1 TO HEIGHT : FOR CLW% = 1 TO WIDTH : PRINT " "; : NEXT : SLEEP 0.000001 : NEXT
  801  IF EXTCOLS% = 0 THEN COLOR BGCOLOR$, FGCOLOR$ : FOR CLH% = 1 TO HEIGHT : FOR CLW% = 1 TO WIDTH : PRINT " "; : NEXT : SLEEP 0.000001 : NEXT
  810  REM PAINT BORDER
  820  Y$ = "1" : X$ = "1" : GOSUB 2090
  830  PRINT STRING$(WIDTH, DBL_HORIZONTAL$);
  840  Y$ = "1" : X$ = "1" : GOSUB 2090 : PRINT DBL_TOP_LEFT$;
  850  Y$ = "1" : X$ = STR$(WIDTH) : GOSUB 2090 : PRINT DBL_TOP_RIGHT$;
  860  Y$ = STR$(HEIGHT - 1) : X$ = "1" : GOSUB 2090 : PRINT DBL_BOTTOM_LEFT$;
  870  Y$ = STR$(HEIGHT - 1) : X$ = STR$(WIDTH) : GOSUB 2090 : PRINT DBL_BOTTOM_RIGHT$;
  880  Y$ = STR$(HEIGHT - 1) : X$ = "2" : GOSUB 2090 : PRINT STRING$(WIDTH - 2, DBL_HORIZONTAL$);
  890  Y$ = "1" : X$ = STR$(INT(WIDTH / 2 )) : GOSUB 2090 : PRINT DBL_T_DOWN$;
  900  Y$ = STR$(HEIGHT - 1) : X$ = STR$(INT(WIDTH / 2)) : GOSUB 2090 : PRINT DBL_T_UP$;
  910  FOR I% = 2 TO HEIGHT - 2
  920  Y$ = STR$(I%) : X$ = "1" : GOSUB 2090 : PRINT DBL_VERTICAL$;
  930  Y$ = STR$(I%) : X$ = STR$(WIDTH) : GOSUB 2090 : PRINT DBL_VERTICAL$;
  940  Y$ = STR$(I%) : X$ = STR$(INT(WIDTH / 2)) : GOSUB 2090 : PRINT DBL_VERTICAL$;
  950  NEXT I%
  960  Y$ = STR$(HEIGHT - 3) : X$ = "1" : GOSUB 2090 : PRINT DBL_T_RIGHT$;
  970  Y$ = STR$(HEIGHT - 3) : X$ = STR$(INT(WIDTH / 2)) : GOSUB 2090 : PRINT DBL_T_LEFT$;
  980  Y$ = STR$(HEIGHT - 3) : X$ = "2" : GOSUB 2090 : PRINT STRING$(INT(WIDTH / 2 - 2), DBL_HORIZONTAL$);
  990  Y$ = STR$(INT(HEIGHT / 4)) : X$ = STR$(INT(WIDTH / 2)) : GOSUB 2090 : PRINT DBL_T_RIGHT$;
 1000  Y$ = STR$(INT(HEIGHT / 4)) : X$ = STR$(WIDTH) : GOSUB 2090 : PRINT DBL_T_LEFT$;
 1010  Y$ = STR$(INT(HEIGHT / 4)) : X$ = STR$(INT(WIDTH / 2) + 1) : GOSUB 2090 : PRINT STRING$(INT(WIDTH / 2) - 1, DBL_HORIZONTAL$);
 1020  GOSUB 1430
 1030  REM FILL
 1040  TOTALPAGES% = INT(MAXSTACK% / MAXPERPAGE%)
 1050  IF (MAXSTACK% / MAXPERPAGE%) > INT(MAXSTACK% / MAXPERPAGE%) THEN TOTALPAGES% = INT(MAXSTACK% / MAXPERPAGE%) + 1
 1060  SHOWORHIDE% = 0 : Y$ = STR$(HEIGHT - 2) : X$ = "3" : GOSUB 2090 : PRINT USER$; " "; DATE$; " "; TIME$; " "; CURRENTPAGE% + 1; "/"; TOTALPAGES% : SHOWORHIDE% = 1
 1070  GOSUB 1140
 1080  GOSUB 1310
 1090  GOSUB 1240
 1100  IF OWIDTH% <> WIDTH OR OHEIGHT% <> HEIGHT THEN REDRAW% = 1
 1110  IF REDRAW% = 1 THEN REDRAW% = 0 : KEY$ = "" : GOTO 2470
 1120  KEY$ = POLKEY$
 1130  GOTO 1030
 1140  REM FILE LIST
 1150  Y$ = "2" : X$ = "3" : GOSUB 2090
 1160  SCREENI% = 3
 1170  FROM% = CURRENTPAGE% * MAXPERPAGE%
 1180  TOO% = FROM% + MAXPERPAGE%
 1190  FOR I% = FROM% TO TOO%
 1200  PRINT ARRAY$(I%); SPC$(8) : Y$ = STR$(SCREENI%) : X$ = "3" : GOSUB 2090
 1210  SCREENI% = SCREENI% + 1
 1220  NEXT I%
 1230  RETURN
 1240  REM ACTIVE
 1250  IF CURRENTPOS% + 2 > HEIGHT THEN RETURN
 1260  Y$ = STR$(CURRENTPOS%+2) : X$ = "3" : GOSUB 2090
 1280  IF EXTCOLS% = 1 THEN PRINT CHR$(27) + "[38;2;" + BG_R$ + ";" + BG_G$ + ";" + BG_B$ + ";48;2;" + FG_R$ + ";" + FG_G$ + ";" + FG_B$ + "m";
 1281  IF EXTCOLS% = 0 THEN COLOR FGCOLOR$, BGCOLOR$
 1282  PRINT ARRAY$(CURRENTPAGE% * MAXPERPAGE% + CURRENTPOS%);
 1290  IF EXTCOLS% = 1 THEN PRINT CHR$(27) + "[38;2;" + FG_R$ + ";" + FG_G$ + ";" + FG_B$ + ";48;2;" + BG_R$ + ";" + BG_G$ + ";" + BG_B$ + "m";
 1291  IF EXTCOLS% = 0 THEN COLOR BGCOLOR$, FGCOLOR$
 1300  RETURN
 1310  REM KEY PRESSED
 1320  IF KEY$ = "s" THEN CURRENTPOS% = CURRENTPOS% + 1 : IF CURRENTPOS% > MAXPERPAGE% OR ARRAY$(CURRENTPAGE% * MAXPERPAGE% + CURRENTPOS%) = "" THEN CURRENTPOS% = 0
 1330  IF KEY$ = "w" THEN CURRENTPOS% = CURRENTPOS% - 1 : IF CURRENTPOS% < 0 THEN CURRENTPOS% = 0
 1340  IF KEY$ = "c" THEN GOSUB 1850
 1350  IF KEY$ = "o" THEN GOSUB 1530
 1360  REMAINDER% = MAXSTACK% MOD MAXPERPAGE%
 1370  IF KEY$ = "." THEN CURRENTPAGE% = CURRENTPAGE% + 1 : CURRENTPOS% = CURRENTPAGE% * MAXPERPAGE% - (MAXPERPAGE% * CURRENTPAGE%) : IF CURRENTPAGE% >= CINT(MAXSTACK% / MAXPERPAGE%) + SGN(REMAINDER%) -1 THEN CURRENTPAGE% = CINT(MAXSTACK% / MAXPERPAGE%) - 1
 1380  IF KEY$ = "," THEN CURRENTPAGE% = CURRENTPAGE% - 1 : CURRENTPOS% = CURRENTPAGE% * MAXPERPAGE% - MAXPERPAGE% : IF CURRENTPAGE% < 0 THEN CURRENTPAGE% = 0
 1390  IF CURRENTPOS% < 0 THEN CURRENTPOS% = 0
 1400  IF KEY$ = "." OR KEY$ = "," THEN GOSUB 1750
 1410  IF KEY$ = "r" THEN REDRAW% = 1
 1420  RETURN
 1430  REM HINT TEXT
 1440  Y$ = "2" : X$ = STR$(INT(WIDTH / 2) + 2) : GOSUB 2090
 1450  PRINT "telecommander";
 1460  Y$ = "3" : X$ = STR$(INT(WIDTH / 2) + 2) : GOSUB 2090
 1470  PRINT "w - move up, s - move down, r - redraw";
 1480  Y$ = "4" : X$ = STR$(INT(WIDTH / 2) + 2) : GOSUB 2090
 1490  PRINT "o - open file, c - copy file";
 1500  Y$ = "5" : X$ = STR$(INT(WIDTH / 2) + 2) : GOSUB 2090
 1510  PRINT ". - next page , - previous page";
 1520  RETURN
 1530  REM PREVIEW FILE
 1540  GOSUB 1690
 1550  GOSUB 2290
 1560  IF ISBINARYFILE% = 1 THEN ISBINARYFILE% = 0 : RETURN
 1570  POSY% = INT(HEIGHT / 4) + 1
 1580  II% = 1
 1590  OPEN ARRAY$(CURRENTPAGE% * MAXPERPAGE% + CURRENTPOS%), AS #1
 1600  IF EOF(1) = -1 OR POSY% > HEIGHT - 2 THEN GOTO 1670
 1610  INPUT# 1, FILECONTENT$
 1620  Y$ = STR$(POSY%) : X$ = STR$(INT(WIDTH / 2) + 3) : GOSUB 2090
 1630  POSY% = POSY% + 1
 1640  II% = II% + 1
 1650  PRINT LEFT$(FILECONTENT$, INT(WIDTH / 2) - 6);
 1660  GOTO 1600
 1670  CLOSE #1
 1680  RETURN
 1690  REM CLEAR FILE PREVIEW
 1700  FOR I% = INT(HEIGHT / 4) + 1 TO HEIGHT - 2
 1710  Y$ = STR$(I%) : X$ = STR$(INT(WIDTH / 2) + 3) : GOSUB 2090
 1720  PRINT SPC$(INT(WIDTH / 2)-3);
 1730  NEXT I%
 1740  RETURN
 1750  REM CLEAR FILE AREA
 1760  LOCATE 2, 3
 1770  FOR I% = 0 TO MAXPERPAGE%
 1780  PRINT SPC$(INT(WIDTH / 2) - 5);
 1790  Y$ = STR$(I% + 3) : X$ = "3" : GOSUB 2090
 1800  NEXT I%
 1810  GOSUB 1140
 1820  CURRENTPOS% = CURRENTPAGE% * MAXPERPAGE% - (MAXPERPAGE% * CURRENTPAGE%)
 1830  GOSUB 1240
 1840  RETURN
 1850  REM COPY FILE
 1860  GOSUB 1690
 1870  SHOWORHIDE% = 1
 1880  Y$ = STR$(INT(HEIGHT / 4) + 2) : X$ = STR$(INT(WIDTH / 2) + 3) : GOSUB 2110
 1890  PRINT "Copy File "; ARRAY$(CURRENTPAGE% * MAXPERPAGE% + CURRENTPOS%); " to...";
 1900  Y$ = STR$(INT(HEIGHT / 4) + 3) : X$ = STR$(INT(WIDTH / 2) + 3) : GOSUB 2110
 1910  INPUT ">", NEWFILE$
 1920  IF NEWFILE$ = "" OR LEN(NEWFILE$) > 11 THEN PRINT CHR$(10); "Max. Length is 8!" : GOTO 1900
 1930  Y$ = STR$(INT(HEIGHT / 4) + 5) : X$ = STR$(INT(WIDTH / 2) + 3) : GOSUB 2110
 1940  PRINT "Copying file "; ARRAY$(CURRENTPAGE% * MAXPERPAGE% + CURRENTPOS%); " to "; NEWFILE$; "...";
 1950  OPEN ARRAY$(CURRENTPAGE% * MAXPERPAGE% + CURRENTPOS%), AS #1
 1960  OPEN NEWFILE$, AS #2
 1970  IF EOF(1) = -1 THEN GOTO 2010
 1980  INPUT# 1, FILELINE$
 1990  PRINT# 2, FILELINE$
 2000  GOTO 1970
 2010  CLOSE #1
 2020  CLOSE #2
 2030  Y$ = STR$(INT(HEIGHT / 4) + 6) : X$ = STR$(INT(WIDTH / 2) + 3) : GOSUB 2090
 2040  ARRAY$(MAXSTACK%) = NEWFILE$
 2050  MAXSTACK% = MAXSTACK% + 1
 2060  PRINT "File has been copied.";
 2070  SHOWORHIDE% = 0 : GOSUB 2420
 2080  RETURN
 2090  REM LOCATE Y X
 2100  SHOWORHIDE% = 0
 2110  GOSUB 2420
 2120  PRINT CHR$(27) + "[" + Y$ + ";" + X$ + "H";
 2130  GOSUB 2420
 2140  RETURN
 2150  REM STATUSBAR FROM UNDERWOOD
 2160  REM INIT
 2170  PRINT "Loading..."; CHR$(10)
 2180  S% = 1
 2190  E% = LEN(DIR$)
 2200  T% = 1
 2210  RETURN
 2220  REM INCREMENT
 2230  MD% = INT((T%/E%*100)/10)
 2240  ML% = 10 - INT((T%/E%*100)/10)
 2250  P% = INT(T%/E%*100)
 2260  IF O% <> P% THEN PRINT CHR$(27) "[A" "[" STRING$(MD%, "#") STRING$(ML%, "_") "] " STR$(P%) "%" : O% = P% : T% = T% + 1 : RETURN
 2270  IF P% <> 100 THEN T% = T% + 1 : RETURN
 2280  RETURN
 2290  REM CHECK FILE TYPE FOR PREVIEW
 2300  FILENAME$ = ARRAY$(CURRENTPAGE% * MAXPERPAGE% + CURRENTPOS%)
 2310  IF INSTR(FILENAME$, ".a2", 0) > -1 THEN GOSUB 2370
 2320  IF INSTR(FILENAME$, ".exe", 0) > -1 THEN GOSUB 2370
 2330  IF INSTR(FILENAME$, ".gam", 0) > -1 THEN GOSUB 2370
 2340  IF INSTR(FILENAME$, ".vt", 0) > -1 THEN GOSUB 2370
 2350  IF INSTR(FILENAME$, ".c8", 0) > -1 THEN GOSUB 2370
 2360  RETURN
 2370  REM CHECK FILE TYPE PRINT ERROR MESSAGE
 2380  GOSUB 1690
 2390  Y$ = STR$(INT(HEIGHT / 4) + 1) : X$ = STR$(INT(WIDTH / 2) + 3) : GOSUB 2090 : PRINT "Binary Files cannot be opened";
 2400  ISBINARYFILE% = 1
 2410  RETURN
 2420  REM SHOW HIDE CURSOR SHOWORHIDE%
 2430  SLEEP 0
 2440  IF SHOWORHIDE% = 1 THEN PRINT CHR$(27) "[?25h";
 2450  IF SHOWORHIDE% = 0 THEN PRINT CHR$(27) "[?25l";
 2460  RETURN
 2470  REM REDRAW
 2480  MAXPERPAGE% = HEIGHT - 6
 2490  OWIDTH% = WIDTH
 2500  OHEIGHT% = HEIGHT
 2510  GOTO 780
